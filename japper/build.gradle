plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

repositories {
    jcenter()
}

dependencies {
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
                'Specification-Title': artifactName,
                'Specification-Version': artifactVersion,
                'Specification-Vendor': vendor,
                'Implementation-Title': artifactName,
                'Implementation-Version': artifactVersion,
                'Implementation-Vendor': vendor
        )
    }
}

publishing {
    repositories {
        maven {
            name "OSSRH"
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            credentials {
                username System.getenv("MAVEN_USERNAME")
                password System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId 'io.github.cleanpojo'
            artifactId artifactId
            version artifactVersion
            from components.java
            pom {
                name = artifactName
                description = artifactDescription
                url = "https://github.com/CleanPojo/Japper"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/CleanPojo/Japper/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "gyuwon"
                        name = "Yi Gyuwon"
                        email = "gyuwon@live.com"
                    }
                }
                scm {
                    connection = "https://github.com/CleanPojo/Japper.git"
                    developerConnection = "https://github.com/CleanPojo/Japper.git"
                    url = "https://github.com/CleanPojo/Japper"
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}
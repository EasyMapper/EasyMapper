import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.7.20"
    id 'maven-publish'
    id 'signing'
}

repositories {
    jcenter()
}

dependencies {
    api "io.github.easymapper:easymapper:[0.3.0, 0.4.0)"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    implementation "org.jetbrains.kotlin:kotlin-reflect"
    testImplementation "org.junit.jupiter:junit-jupiter-api:5.6.0"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:5.6.0"
    testImplementation "org.assertj:assertj-core:3.23.1"
    testImplementation "io.github.autoparams:autoparams:3.1.0"
    testImplementation "io.github.autoparams:autoparams-kotlin:3.1.0"
}

tasks.withType(KotlinCompile).configureEach {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

test {
    useJUnitPlatform()
}

java {
    withJavadocJar()
    withSourcesJar()
}

jar {
    manifest {
        attributes(
            'Specification-Title': artifactName,
            'Specification-Version': artifactVersion,
            'Specification-Vendor': vendor,
            'Implementation-Title': artifactName,
            'Implementation-Version': artifactVersion,
            'Implementation-Vendor': vendor
        )
    }
}

publishing {
    repositories {
        maven {
            name "OSSRH"
            url "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username System.getenv("MAVEN_USERNAME")
                password System.getenv("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId 'io.github.easymapper'
            artifactId artifactId
            version artifactVersion
            from components.java
            pom {
                name = artifactName
                description = artifactDescription
                url = "https://github.com/EasyMapper/EasyMapper"
                licenses {
                    license {
                        name = "MIT License"
                        url = "https://github.com/EasyMapper/EasyMapper/blob/main/LICENSE"
                    }
                }
                developers {
                    developer {
                        id = "gyuwon"
                        name = "Yi Gyuwon"
                        email = "gyuwon@live.com"
                    }
                }
                scm {
                    connection = "https://github.com/EasyMapper/EasyMapper.git"
                    developerConnection = "https://github.com/EasyMapper/EasyMapper.git"
                    url = "https://github.com/EasyMapper/EasyMapper"
                }
            }
        }
    }
}

signing {
    def signingKey = System.getenv("SIGNING_KEY")
    def signingPassword = System.getenv("SIGNING_PASSWORD")
    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications.maven
}
